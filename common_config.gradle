apply from: "${rootProject.rootDir}/versions.gradle"

project.ext {
    //设置application默认配置
    setApplicationConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.description "app"
            setAndroidConfig extension.android
            //setDependencies extension.dependencies
    }

    //设置LibDefault默认配置
    setLibraryConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.description "library"
            setAndroidConfig extension.android
            setDependencies extension.dependencies
    }


    //设置Android配置
    setAndroidConfig = {
        extension ->
            extension.compileSdkVersion build_versions.compileSdkVersion
            extension.buildToolsVersion build_versions.buildToolsVersion

            extension.defaultConfig {
                minSdkVersion build_versions.minSdkVersion
                targetSdkVersion build_versions.targetSdkVersion

                //Arouter配置
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [moduleName: project.getName()]
                    }
                }
            }

            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            extension.lintOptions {
                abortOnError false
            }
    }


    //依赖库配置
    setDependencies = {
        extension ->
            extension.implementation fileTree(dir: 'libs', include: ['*.aar,*.jar'])

            // Dependencies for local unit tests
            extension.testImplementation deps.junit
            extension.testImplementation deps.hamcrest
            extension.testImplementation deps.arch_core.testing
            extension.testImplementation deps.mockito.core

            // Android Testing Support Library's runner and rules
            extension.androidTestImplementation deps.atsl.ext_junit
            extension.androidTestImplementation deps.atsl.runner
            extension.androidTestImplementation deps.atsl.rules
            extension.androidTestImplementation deps.room.testing
            extension.androidTestImplementation deps.arch_core.testing

            // Dependencies for Android unit tests
            extension.androidTestImplementation deps.junit
            extension.androidTestImplementation deps.mockito.core, { exclude group: 'net.bytebuddy' }
            extension.androidTestImplementation deps.dexmaker

            // Espresso UI Testing
            extension.androidTestImplementation deps.espresso.core
            extension.androidTestImplementation deps.espresso.contrib
            extension.androidTestImplementation deps.espresso.intents

            // Resolve conflicts between main and test APK:
            //extension.androidTestImplementation deps.annotations
            //extension.androidTestImplementation deps.app_compat
            //extension.androidTestImplementation deps.material
    }
}